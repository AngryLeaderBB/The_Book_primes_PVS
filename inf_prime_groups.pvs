%------------------------------------------------------------------------------
%  Proof of the infinity of prime numbers using the multiplicative group
%  Z_q, where q is a prime number, and Lagrange's Theorem. 
%
%  Formalization based on the reference "Proofs from the Book", 
%  Martin Aigner Günter and M. Ziegler
%
% Authors:
%               Marcos Rony Silva Brandão
%               Universidade Federal de Goiás - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
%               Mariano M. Moscato
%               NASA  Langley  Research  Center - US
%
%------------------------------------------------------------------------------

inf_prime_groups: THEORY

BEGIN
IMPORTING ints@max_finite_set_nat,
          ints@primes,
          algebra@finite_groups,
          algebra_examples@ring_zn,
          algebra@quotient_rings[int, +, *, 0],
	  finite_groups_extra,
	  finite_sets@finite_sets_card_eq,
	  ring_zn_extra,
	  fermats_little_theorem_extra
	  
	
	  
q: VAR (prime?)   
n: VAR posnat
a: VAR int
i: VAR nat

%S: VAR non_empty_finite_set[nat]

%max(S) : ({a: nat | S(a) AND (FORALL i: S(i) IMPLIES i <= a)}) = max_finite_set_nat.max(S)
Primes: set[nat] = {x:nat | prime?(x)}
nz_coset(n): Type = {x: coset(Z,nZ(n))| x /= nZ(n)}

% In the next Conjecture,  we have two important remarks:
% (i) Since we have two use in subsequent lemmas that we are dealing with a multiplicative group Z_q- {nZ(q)},
% we prove the next lemma over nz_cosets.
% (ii) Also, a TCC is generated from such conjecture: the product of two nz_cosets is also a nz_coset.
% It holds in case we are considering the cosets over nZ(q), where q is a prime number.

two_not_zero_coset: LEMMA
   FORALL(n: posnat| n > 1):
       odd?(n) => 2+nZ(n) /= nZ(n)

nz_coset_product_is_nz_coset: LEMMA
  FORALL (n: posnat, x1: [nz_coset(n), nz_coset(n)]):
    prime?(n) =>
     product[int, restrict[[numfield, numfield], [int, int], numfield](+),
             restrict[[numfield, numfield], [int, int], numfield](*)]
            (Z, nZ(n))(x1)
         /= nZ(n)


power_zp_nz_coset(p:(prime?), a: nz_coset(p), k:nat): {pp: nz_coset(p)| pp = 2+nZ(p) IMPLIES pp /= nZ(p)} =
                  power[nz_coset(p), product(Z,nZ(p)),+(1, nZ(p))](a, k)

nz_coset_power_prod_nat: LEMMA
       NOT divides(q,a) IMPLIES 
              power_zp_nz_coset(q, a + nZ(q), i)  = power[int, *, 1](a,i) + nZ(q)

fin_prime_exists_max: LEMMA
is_finite(Primes) IMPLIES Primes(max(Primes))

one_prime_decomposition_ind: LEMMA  %% try to include in a general theory about integers
FORALL (n:nat) : n /= 1 IMPLIES EXISTS(p:(prime?)): divides(p,n)

max_prime_Mersenne_divisor: LEMMA
   is_finite(Primes) IMPLIES EXISTS (q:(prime?)): divides(q, expt(2,max(Primes)) - 1)

   
nz_prime_is_group: LEMMA
		    FORALL (n:posnat): (prime?(n)) IMPLIES
		    	   group?[nz_coset(n),product(Z, nZ(n)), (+)(1, nZ(n))](Z(n))
			   
nz_prime_is_finite_group: LEMMA
			  FORALL (n:posnat): (prime?(n)) IMPLIES
		    	   	 finite_group?[nz_coset(n),product(Z, nZ(n)), (+)(1, nZ(n))](remove[set[int]](nZ(n), Z(n)))

card_mult_group_Zn: LEMMA
card(remove(nZ(n),Z(n))) = n - 1
			   
% - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

% Avoids inst related TCC
power_eq_one_nz_coset(p:(prime?), a:nz_coset(p)): set[posnat] = {n: posnat| power_zp_nz_coset(p, a, n) = 1+nZ(p)}

% Avoids inst related TCC
period_nz_coset(p:(prime?), a:nz_coset(p)): posnat = min(power_eq_one_nz_coset(p, a))

nz_coset_period_def_equiv: LEMMA
   FORALL(p:(prime?), a:nz_coset(p)):
     period[nz_coset(p),product(Z,nZ(p)),+(1,nZ(p))]
	(remove(nZ(p),Z(p)), a) = period_nz_coset(p, a) 

period_2_Zq_is_max_prime_lemma1: LEMMA
   is_finite(Primes) AND (odd?(q)) AND  divides(q, expt(2,max(Primes)) - 1) IMPLIES
      power_zp_nz_coset(q, +(2,nZ(q)), max(Primes)) = +(1,nZ(q))
      
period_2_Zq_is_max_prime_lemma2: LEMMA
    is_finite(Primes) IMPLIES (FORALL (q:(prime?)|odd?(q)): divides(q, expt(2,max(Primes)) - 1) IMPLIES
     period_nz_coset(q, +(2,nZ(q))) = max(Primes)) 

fin_prime_q_gt_maxp: LEMMA
is_finite(Primes) IMPLIES EXISTS (q:(prime?)|odd?(q)): q > max(Primes)


primes_is_infinity: THEOREM
NOT is_finite(Primes)


END inf_prime_groups
